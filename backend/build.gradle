buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/vaadin-snapshots/"}
        maven { url "https://maven.eveoh.nl/content/repositories/releases" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("nl.eveoh:gradle-aspectj:1.6")
    }
}

plugins {
    id "net.ltgt.apt" version "0.10"
    id 'fi.jasoft.plugin.vaadin' version '1.1.10'
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
/*project.ext {
    aspectjVersion = '1.8.4'
}
apply plugin: 'aspectj'*/

group 'com.faendir'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = 1.8
ext {
    debug = true
}

dependencies {
    //core
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile "org.springframework.boot:spring-boot-starter-cache"
    compile "org.springframework:spring-aspects:4.3.8.RELEASE"
    compile 'com.vaadin:vaadin-spring-boot-starter'
    //addons
    compile 'org.vaadin.addons:popupbutton:3.0.0'
    compile 'org.vaadin.addon:jfreechartwrapper:4.0.0'
    compile 'org.vaadin.addons:stepper:2.4.0'
    //utility
    compile 'org.codeartisans:org.json:20161124'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'commons-fileupload:commons-fileupload:1.3.2'
    compile 'org.ocpsoft.prettytime:prettytime:3.2.7.Final'
    compile 'net.sf.proguard:proguard-retrace:5.3.3'
    compile ('org.jfree:jfreechart:1.0.19') {
        exclude group:'javax.servlet', module:'servlet-api'
    }
}

configurations {
    'vaadin-client' {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('javax.validation:validation-api') with module('javax.validation:validation-api:1.0.0.GA')
            }
            //force 'javax.validation:validation-api:1.0.0.GA'
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:8.1-SNAPSHOT"
    }
}

bootRun {
    // Needed by Vaadin plugin (https://github.com/johndevs/gradle-vaadin-plugin/issues/325)
    classpath += configurations['vaadin-server']
    classpath += configurations['vaadin-client']
    jvmArgs = ["-javaagent:org.springframework.instrument.jar"]

    dependsOn 'vaadinCompile', 'vaadinThemeCompile'
}

project.convention.getPlugin(WarPluginConvention).webAppDirName = "src/main/resources"

if (!ext.debug) {
    apply from: 'releasepackaging.gradle'
}